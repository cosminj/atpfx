buildscript {
    ext {
        springBootVersion = '1.2.2.RELEASE'
        springSecurityOauth2Version = '2.0.6.RELEASE'
    }
    repositories {
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"        
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repository.jboss.org/nexus/content/groups/public' }
    flatDir {
        dirs 'libs'
    }
}

jar {
    baseName = 'atpfx'
    version = '0.0.1-SNAPSHOT'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    compile name: 'fxcm-api'
    compile name: 'fxmsg'

    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-ws"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"

    // not included in boot
    compile "org.springframework.security.oauth:spring-security-oauth2:${springSecurityOauth2Version}"

    compile("postgresql:postgresql:+")
    compile 'com.google.guava:guava:+'

    testCompile 'cglib:cglib-nodep:+'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.codehaus.groovy:groovy-all:+'
    testCompile 'org.spockframework:spock-core:+'
    testCompile 'org.spockframework:spock-spring:+'
    testCompile 'com.h2database:h2:+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir 
    rename {
        'atpfx.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('atpfx.jar').delete()
}

mainClassName = "com.atpfx.Atpfx"